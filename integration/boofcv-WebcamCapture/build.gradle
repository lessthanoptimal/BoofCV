
dependencies {
    implementation project(':main:boofcv-ip')
    implementation project(':main:boofcv-io')
    implementation project(':integration:boofcv-swing')

    compile group: 'com.github.sarxos', name: 'webcam-capture', version: '0.3.12'

    testCompile project(':main:boofcv-geo')
    testCompile project(':main:boofcv-feature')
    testCompile project(':main:boofcv-recognition')
}

sourceSets {
    test {
        java {
            srcDir 'test'
            srcDir 'examples'
        }
        resources {
            srcDir 'resources/test'
        }
    }
}

// Runs an application from the examples directory
// gradle webcamRun -Pwhich=boofcv.examples.ExampleTrackingKlt
task webcamRun(dependsOn: [classes,testClasses] ) {
    doLast {
        if (project.hasProperty('which')) {
            javaexec {
                main = "$which"
                classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
            }
        } else {
            print 'Which not specified!!!!!'
        }
    }
}