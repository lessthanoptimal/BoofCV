// TODO insert analytics and adsense into javadoc

ext.libpath = file('./').absolutePath

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group = 'org.boofcv'
    version = '0.18'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.6

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDir 'resources/src'
            }
        }

        test {
            java {
                srcDir 'test'
                srcDir 'generate'
                srcDir 'benchmark'
            }
            resources {
                srcDir 'resources/test'
            }
        }
    }

    dependencies {
//        compile files("$libpath/lib/EJML.jar")
//        compile files("$libpath/lib/DDogleg.jar")
//        compile files("$libpath/lib/GeoRegression.jar")

        compile group: 'org.georegression', name: 'georegression', version: '0.7'

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    jar {
        manifest { // the manifest of the default jar is of type OsgiManifest
            instruction 'Bundle-Vendor', 'BoofCV'
//            instruction 'Bundle-Description', 'BoofCV'
            instruction 'Bundle-DocURL', 'http://boofcv.org'
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    // if Maven central isn't setup in gradle.properties skip all of this
    if( project.hasProperty('ossrhUsername') ) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'BoofCV'
                        packaging 'pom'
                        // optionally artifactId can be defined here
                        description 'BoofCV is an open source Java library for real-time computer vision and robotics applications.'
                        url 'http://boofcv.org'

                        scm {
                            connection 'scm:git:git://github.com/lessthanoptimal/BoofCV.git'
                            developerConnection 'scm:git:git://github.com/lessthanoptimal/BoofCV.git'
                            url 'https://github.com/lessthanoptimal/BoofCV'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                id 'pabeles'
                                name 'Peter Abeles'
                                email 'peter.abeles@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

// list of projects for creating javadoc and jars
def mainProjects = [
        ':main:ip',
        ':main:io',
        ':main:feature',
        ':main:geo',
        ':main:calibration',
        ':main:sfm',
        ':main:recognition',
        ':main:visualize',
]

def integrationProjects = [
        ':integration:WebcamCapture',
        ':integration:xuggler',
]
try {
    project(':integration:android')
    integrationProjects.add(':integration:android')
} catch( UnknownProjectException ignore ) {}
try {
    project(':integration:openkinect')
    integrationProjects.add(':integration:openkinect')
} catch( UnknownProjectException ignore ) {}

def javadocProjects = mainProjects + integrationProjects

// Creates a directory with all the compiled BoofCV jars and the dependencies for main
task createLibraryDirectory( dependsOn: javadocProjects.collect {[ it+':jar',it+':sourcesJar']}.flatten() ) << {

    // dependencies for main and their own jars
    ext.listExternal = files(mainProjects.collect{ project(it).configurations.compile })
    // just the compiled jars for integration
    ext.listInternal = files(javadocProjects.collect{ project(it).tasks.jar.archivePath })
    // Add source jars
    ext.listSource = files(javadocProjects.collect{ project(it).tasks.sourcesJar.archivePath })

    ext.listExternal = ext.listExternal - ext.listInternal

    file('libraries').deleteDir()
    file('libraries').mkdir()

    copy {
        from ext.listExternal
        into 'libraries'
    }

    copy {
        from ext.listInternal
        from ext.listSource
        into 'libraries'

        // append on BoofCV so it's clear which jars are part of BoofCV and which are not
        rename { String fileName ->
            "BoofCV-" + fileName
        }
    }
}

task alljavadoc(type: Javadoc) {
    // only include source code in src directory to avoid including 3rd party code which some projects do as a hack
    source = javadocProjects.collect { project(it).fileTree('src').include('**/*.java') }
//    source = javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })

    destinationDir = file("${buildDir}/docs/javadoc")
    configure(options) {
        docTitle = "BoofCV JavaDoc ($project.version)"
        links = [ 'http://docs.oracle.com/javase/7/docs/api/',
                  'http://ejml.org/javadoc/',
                  'http://georegression.org/javadoc/',
                  'http://ddogleg.org/javadoc/']
        header = file('misc/header.txt').text
        // TODO still has issues with quotes
        bottom = file('misc/bottom.txt').text
    }
}

idea {
    project {
        jdkName = '1.6 (64bit)'
        languageLevel = '1.6'
    }

    module {
        excludeDirs += file('.idea')
    }
}

// Disable the creation of jars for distribution.  If you don't do this it will crash
[':main',':examples',':main:checks',':integration',':applications'].each {String a ->
    project(a) {
        if( project.hasProperty('ossrhUsername') ) {
            signArchives.enabled = false
        }
        sourcesJar.enabled = false
        javadocJar.enabled = false
        jar.enabled = false
        uploadArchives.enabled = false
        install.enabled = false
    }
}


