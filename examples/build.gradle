
dependencies {
    api project(':main:boofcv-ip')
    api project(':main:boofcv-io')
    api project(':main:boofcv-feature')
    api project(':main:boofcv-geo')
    api project(':main:boofcv-calibration')
    api project(':main:boofcv-sfm')
    api project(':main:boofcv-learning')
    api project(':main:boofcv-recognition')
    api project(':integration:boofcv-swing')

    implementation project(':integration:boofcv-ffmpeg')
    implementation project(':integration:boofcv-WebcamCapture')

    api 'org.reflections:reflections:0.9.10'
}

// Only add javafx as a dependency if it exists. The code will run just fine without it,
// but some visualizations look better
try {
    project.project(':integration:boofcv-javafx')
    dependencies {
        compile project(':integration:boofcv-javafx')
    }
} catch( UnknownProjectException ignore ) {}

idea {
    module {
        name = "BoofCV Examples"
    }
}

// Create a jar which contains all the applications
task examples(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'BoofCV Examples Jar',
                'Implementation-Version': version,
                'Main-Class': 'boofcv.examples.ExampleLauncherApp'
    }

    outputs.upToDateWhen { false }
    
    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    doLast {
        archivePath.renameTo(file('examples.jar'))
    }
}